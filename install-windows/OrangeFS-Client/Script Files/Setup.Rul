//===========================================================================
//
//  File Name:    Setup.rul
//
//  Description:  Blank setup main script file
//
//  Comments:     Blank setup is an empty setup project. If you want to
//				  create a new project via. step-by step instructions use the
//				  Project Assistant.
//
//===========================================================================

// Included header files ----------------------------------------------------
#include "ifx.h" 
#include "MountPointUserModeDialog.Rul" 
#include "ListMapDialog.Rul"

// Note: In order to have your InstallScript function executed as a custom
// action by the Windows Installer, it must be prototyped as an 
// entry-point function.

// The keyword export identifies MyFunction() as an entry-point function.
// The argument it accepts must be a handle to the Installer database.
    
/* export prototype MyFunction(HWND); */

prototype InstallServices(); 
prototype InstallDriver(); 
prototype WriteOrangeFSTab(STRING);

// global variables
#define USERMAP_LIST     1
#define USERMAP_CERT     2
#define USERMAP_LDAP     3

// 64-bit flag
BOOL bIs64Bit;
// Program Files and System directories for appropriate architecture
STRING szProgramFilesDir, szSystemDir;

//---------------------------------------------------------------------------
// OnBegin
//
// The OnBegin event is called directly by the framework after the setup
// initializes.
//---------------------------------------------------------------------------
function OnBegin()
begin
    // Set directory locations based on architecture (32/64-bit)
    bIs64Bit = SYSINFO.bIsWow64;
    if (bIs64Bit) then
        szProgramFilesDir = PROGRAMFILES64;
        szSystemDir = WINSYSDIR64;
    else
        szProgramFilesDir = PROGRAMFILES;
        szSystemDir = WINSYSDIR;
    endif;
end;
//---------------------------------------------------------------------------
// OnFirstUIBefore
//
// The OnFirstUIBefore event is called by the framework when the setup is
// running in first install mode. By default this event displays UI allowing
// the end user to specify installation parameters.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
    NUMBER nResult, nSetupType, nvSize, nUser;
    STRING szTitle, szMsg, szQuestion, svName, svCompany, szFile;
    STRING szLicenseFile;
	BOOL bCustom, bIgnore1, bIgnore2;
begin	
    // TO DO: if you want to enable background, window title, and caption bar title                                                                   
    // SetTitle( @PRODUCT_NAME, 24, WHITE );                                        
    // SetTitle( @PRODUCT_NAME, 0, BACKGROUNDCAPTION ); 	                  
    // Enable( FULLWINDOWMODE );						   
    // Enable( BACKGROUND );							  
    // SetColor(BACKGROUND,RGB (0, 128, 128));					   

    // Added in InstallShield 15 - Show an appropriate error message if
    // -removeonly is specified and the product is not installed.
    if( REMOVEONLY ) then
        Disable( DIALOGCACHE );
		szMsg = SdLoadString( IDS_IFX_ERROR_PRODUCT_NOT_INSTALLED_UNINST );
   		SdSubstituteProductInfo( szMsg );
		MessageBox( szMsg, SEVERE );
		abort;
    endif;
    
	nSetupType = TYPICAL;	

Dlg_SdWelcome:
    szTitle = "";
    szMsg   = "";
    nResult = SdWelcome(szTitle, szMsg);
    if (nResult = BACK) goto Dlg_SdWelcome;
	
	szTitle   = "";
	svName    = "";
    svCompany = "";
/*
Dlg_SdRegisterUser:
    szMsg = "";
    szTitle = "";
    nResult = SdRegisterUser( szTitle, szMsg, svName, svCompany );
    if (nResult = BACK) goto Dlg_SdWelcome;
*/
Dlg_SetupType:
    szTitle = "";
    szMsg   = "";
    nResult = SetupType2(szTitle, szMsg, "", nSetupType, 0);
    if (nResult = BACK) then
        goto Dlg_SdWelcome;
    else
	    nSetupType = nResult;
        if (nSetupType != CUSTOM) then
	        nvSize = 0;
	        FeatureCompareSizeRequired(MEDIA, INSTALLDIR, nvSize);
	        if (nvSize != 0) then      
            	MessageBox(szSdStr_NotEnoughSpace, WARNING);
	            goto Dlg_SetupType;
            endif;
			bCustom = FALSE;
			goto Dlg_SdStartCopy;
		else
			bCustom = TRUE;
        endif;
    endif;    

Dlg_SdAskDestPath:    	
    nResult = SdAskDestPath(szTitle, szMsg, INSTALLDIR, 0);
    if (nResult = BACK) goto Dlg_SetupType;

Dlg_SdFeatureTree: 
    szTitle    = "";
    szMsg      = "";
    if (nSetupType = CUSTOM) then
		nResult = SdFeatureTree(szTitle, szMsg, INSTALLDIR, "", 2);
		if (nResult = BACK) goto Dlg_SdAskDestPath;  
    endif;

Dlg_SdStartCopy:
    szTitle = "";
    szMsg   = "";
    nResult = SdStartCopy2( szTitle, szMsg );			
	
    if (nResult = BACK) then
       goto Dlg_SdFeatureTree;
    endif;

    // Added in IS 2009 - Set appropriate StatusEx static text.
    SetStatusExStaticText( SdLoadString( IDS_IFX_STATUSEX_STATICTEXT_FIRSTUI ) );

    // setup default status
    Enable(STATUSEX);
 
    return 0;
end;                    
            
// Output the file "orangefstab" with the file system location URI in it
function WriteOrangeFSTab(szFSURI)
    NUMBER nFileHandle;
begin
    OpenFileMode(FILE_MODE_APPEND);
    
    if (CreateFile(nFileHandle, INSTALLDIR, "orangefstab") < 0) then
        MessageBox("File " + INSTALLDIR + "\\orangefstab could not be created. " +
                   "You must add this finally manually (see documentation).", SEVERE);
    else
        WriteLine(nFileHandle, szFSURI + " /mnt/pvfs2 pvfs2 defaults,noauto 0 0"); 
        
        CloseFile(nFileHandle);
    endif;
end;

function InstallDriver()
    STRING svExePath;
begin                                                 
    svExePath = szProgramFilesDir ^ "\\Dokan\\DokanLibrary\\dokanctl.exe";
    
    LaunchAppAndWait(svExePath, "/i d", LAAW_OPTION_WAIT);
end;    

function InstallServices()
    NUMBER rc, line, error;
    STRING file, errText;
begin
    if (!ServiceExistsService("DokanMounter")) then
        rc = ServiceAddService("DokanMounter",
                               "DokanMounter",
                               "OrangeFS/Dokan driver interface service",
                               szProgramFilesDir ^ "\\Dokan\\DokanLibrary\\mounter.exe",
                               TRUE,
                               "");
        if (rc < ISERR_SUCCESS) then
            GetExtendedErrInfo(file, line, error);
            Sprintf(errText, " (%d)", error);
            MessageBox("DokanMounter service installation failed:\n" +
                        FormatMessage(error) + errText + " " +
                        szProgramFilesDir + "\\Dokan\\DokanLibrary\\mounter.exe", SEVERE);
        endif;
    endif;
    if (!ServiceExistsService("orangefs-client")) then
        rc = ServiceAddService("orangefs-client",
                               "OrangeFS Client",
                               "Allows transparent access to Orange file systems.",
                               INSTALLDIR ^ "\\orangefs-client -service",
                               FALSE,
                               "");
        if (rc < ISERR_SUCCESS) then
            GetExtendedErrInfo(file, line, error);
            Sprintf(errText, " (%d)", error);
            MessageBox("OrangeFS Client service installation failed:\n" +
                        FormatMessage(error) + errText + " " +
                        INSTALLDIR + "\\orangefs-client -service", SEVERE);
        endif;
    endif;
end;
                        
//---------------------------------------------------------------------------
// OnFirstUIAfter
//
// The OnFirstUIAfter event called by the framework after the file transfer
// of the setup when the setup is running in first install mode. By default
// this event displays UI that informs the end user that the setup has been
// completed successfully.
//---------------------------------------------------------------------------
function OnFirstUIAfter()
    STRING szTitle, szMsg1, szMsg2, szOpt1, szOpt2;
    STRING szFSURI, szMountPoint, szUserMapMode;
    STRING szUserID, szUID, szGID;
    NUMBER bOpt1, bOpt2, nUserMapMode, nRC;    
begin
	Disable(STATUSEX);

	bOpt1   = FALSE;
    bOpt2   = FALSE;
    
Dlg_MountPointUserMap:
    nUserMapMode = 0;
    nRC = MountPointUserModeDialog(szFSURI, szMountPoint, nUserMapMode);
    
Dlg_UserMap:
    switch(nUserMapMode)
        case USERMAP_LIST:
            nRC = ListMapDialog(szUserID, szUID, szGID); 
            MessageBox(szUserID + " " + szUID + " " + szGID, INFORMATION);
        /*
        case USERMAP_CERT:
            // TODO
        case USERMAP_LDAP:
            // TODO
        */
    endswitch;
    
    if (nRC = BUTTON_BACK) then
        goto Dlg_MountPointUserMap;
    endif;
        
    WriteOrangeFSTab(szFSURI);
    
    InstallServices();
    
    InstallDriver();    

    if ( BATCH_INSTALL ) then
    	SdFinishReboot ( szTitle , szMsg1 , SYS_BOOTMACHINE , szMsg2 , 0 );
    else
	    SdFinish ( szTitle , szMsg1 , szMsg2 , szOpt1 , szOpt2 , bOpt1 , bOpt2 );
	endif;
end;